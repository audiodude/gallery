---
title: Temple
---
<script type="application/processing">
class Blob {
  float x, y;
  int a = 1;
  int sub_mult = 1;
  boolean spawned = false;
  int inc = 1;
  int phase = 0;
  ArrayList<Blob> sub_blobs = new ArrayList<Blob>();
  
  public Blob(float x, float y) {
    this(x, y, 1, 0);
  }
  
  public Blob(float x, float y, int sub_mult, int phase) {
    this.x = x;
    this.y = y;
    this.sub_mult = sub_mult;
    this.phase = phase;
  }
  
  public void display() {
    fill((a+phase)%8*8+color_mat[0], (a+phase)%8*8+color_mat[1], (a+phase)%8*8+color_mat[2]);
    rect(x, y, a*unit/2, a*unit/2);
    for( Blob b : sub_blobs) {
      b.display();
      b.evolve();
    }
  }
  
  public void evolve() {
    if (a == 20/sub_mult && sub_mult < 4 && !spawned) {
      float new_x = x - a*unit/4;
      float new_y = y - a*unit/4;
      if(new_x < width && new_x > 0 && new_y < height && new_y > 0) {
        sub_blobs.add(new Blob(new_x, new_y, sub_mult+1, phase+1));
      }
      
      new_x = x - a*unit/4;
      new_y = y + a*unit/4;
      if(new_x < width && new_x > 0 && new_y < height && new_y > 0) {
        sub_blobs.add(new Blob(new_x, new_y, sub_mult+1, phase+2));
      }
      
      new_x = x + a*unit/4;
      new_y = y + a*unit/4;
      if(new_x < width && new_x > 0 && new_y < height && new_y > 0) {
        sub_blobs.add(new Blob(new_x, new_y, sub_mult+1, phase+3));
      }
      
      new_x = x + a*unit/4;
      new_y = y - a*unit/4;
      if(new_x < width && new_x > 0 && new_y < height && new_y > 0) {
        sub_blobs.add(new Blob(new_x, new_y, sub_mult+1, phase+4));
      }
      
      a--;
      spawned = true;
    } else if(a == 20 || a == 0) {
      inc *= -1;
    }
    a += inc;
  }
}


ArrayList<Blob> blobs = new ArrayList<Blob>();
int unit = 30;
int[] color_mat = {0, 0, 127};
void setup() {
  size(unit*40, unit*20);
  rectMode(CENTER);
  strokeWeight(4);
  stroke(200);
  background(0);
  frameRate(14);

  blobs.add(new Blob(width/4, height/2));
  blobs.add(new Blob(width*3/4, height/2));
}

int iterations=0;
void draw() {                                       
  for (Blob blob : blobs) {
    blob.display();
    blob.evolve();
  }

  if(frameCount % 140 == 0) {
    if(iterations % 3 == 0) {
      color_mat[0] = 127; color_mat[1] = 0; color_mat[2] = 0;
    } else if(iterations % 3 == 1) {
      color_mat[0] = 0; color_mat[1] = 127; color_mat[2] = 0;
    } else if(iterations % 3 == 2) {
      color_mat[0] = 0; color_mat[1] = 0; color_mat[2] = 127;
    }
    iterations++;
  }
}
</script>
