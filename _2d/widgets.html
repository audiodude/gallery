---
title: Widgets
---
<script type="application/processing">
class Widget {
  float len = sqrt(width*height/widgets.length/widgets[0].length);
  float top = 0;
  float x;
  float y;
  int inc = 1;
  boolean up = (random(0, 1) > 0.5);
  boolean right = (random(0, 1) > 0.5);
  color col;
  
  public Widget(float x, float y) {
    this.x = x; this.y = y;
    col = color(x%255, y%255, x*y%255);
  }

  public void flip() {
    up = !up;
    right = !right;
  }


  public void update() {
    top += inc;
    if(top >= len) {
      top = len;
      inc *= -1;
    } else if(top <= 0) {
      top = 0;
      inc *= -1;
    }
  } 
 
  public void display() {
    stroke(col);
    float side = len - top;
    if(up) {
      line(x, y, x, y+top);
    } else {
      line(x, y, x, y-top);
    }
    
    if(right) {
      line(x, y, x+side, y);
    } else {
      line(x, y, x-side, y);
    }

    update();
  } 
}

Widget[][] widgets = new Widget[20][20];

void setup() {
  size(1200, 675);
  stroke(0);
  strokeWeight(4);
  frameRate(30);
  smooth();
  
  for(int i=0; i<widgets.length; i++) {
    for(int j=0; j<widgets[i].length; j++) {
      widgets[i][j] = new Widget(i*width/(widgets.length-1), j*height/(widgets[0].length-1));
    }
  }
}

int a=0;
void draw() {
  background(140);
  for(int i=0; i<widgets.length; i++) {
    for(int j=0; j<widgets[i].length; j++) {
      widgets[i][j].display();
      if(random(0,1) > 0.7) {
        widgets[int(random(widgets.length))][int(random(widgets[0].length))].flip();
      }
    }
  }
  
  a++;
  if(a >= widgets.length*widgets[0].length) {
    a = 0;
  }
}
</script>
